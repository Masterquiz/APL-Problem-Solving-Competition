 Divvy←{vec←⍵⊂⍨⌽(⍳≢⍵)∊⌊(⍳⍺)×⍺÷⍨≢⍵
     vec,(⍺-⍴vec)/⊂⍬                    ⍝ Add remaining empty item
 }

 DivvyTest←{
     Assert←{⍺←'Test failed' ⋄ 0∊⍵:⍺ ⎕SIGNAL 8 ⋄ shy←0}
     Assert((1 2)(3 4),1⊂5)((1 2)(3 4)(5 6))((1 2 3)(4 5)(6 7))((1 2 3)(4 5 6)(7 8))((1 2 3)(4 5 6)(7 8 9))≡3 Divvy¨⍳¨5 6 7 8 9:
     Assert(1⊂1)≡1 Divvy 1:
     Assert'Dy' 'al' 'og' ' A' 'PL'≡5 Divvy'Dyalog APL':
     Assert(4/⊂⍬)≡4 Divvy'':
     Assert((1⊂'A' 'P' 'L'),2/⊂⍬)≡5 Divvy'APL':
     Assert(1⊂1)((1⊂1),⊂⍬)((1⊂1),2/⊂⍬)≡1 2 3 Divvy¨1:
     Assert((1,'A',2,'B')(3,'C',4)('D',5,'E'))≡3 Divvy,(⍳5),[1.1]'ABCDE':
     'Test passed'
 }
