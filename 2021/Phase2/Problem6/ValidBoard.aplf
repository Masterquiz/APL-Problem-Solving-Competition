 ValidBoard←{
     size fleet board←⍺⍺ ⍵⍵ ⍵
     pos←⍸¨board∘=¨⍳⌈/0,,board          ⍝ Positions of each ship
     res←size≡⍴board                    ⍝ Check size
     res,←fleet≡≢¨pos                   ⍝ Check on fleet, number and length
     ∧/res,1=∊{+/¨|1↓2-/⍵,⍨⊂0 1}¨pos↑⍨≢fleet ⍝ Ship in contiguous cells (Without extra ships)
 }

 ValidBoardTest←{
     Assert←{⍺←'Test failed' ⋄ 0∊⍵:⍺ ⎕SIGNAL 8 ⋄ shy←0}
     Assert 1=3 3 ValidBoard 2 3 2 1⊢3 3⍴2 2 2 1 3 3 1 0 4:
     Assert 0=3 3 ValidBoard 2 3 2 1⊢3 3⍴2 2 2 1 3 3 0 1 4:
     Assert 0=3 3 ValidBoard 2 3 2 1⊢3 3⍴2 2 2 1 3 3 1 5 4:
     Assert 0=3 3 ValidBoard 2 3 2 1⊢3 3⍴0 2 2 1 3 3 1 0 4:
     Assert 1=3 3 ValidBoard ⍬⊢3 3⍴0:
     Assert 0=4 3 ValidBoard 2 3 2 1⊢3 3⍴2 2 2 1 3 3 1 0 4:
     Assert 0=3 3 ValidBoard 2 3 2 1⊢3 3⍴2 2 2 3 1 3 0 1 4:
     Assert 0=0 0 ValidBoard 2 3 2 1⊢0 0⍴0:
     Assert 1=0 0 ValidBoard ⍬⊢0 0⍴0:
     'Test passed'
 }
